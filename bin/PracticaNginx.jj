/**
 * JavaCC template file created by SF JavaCC plugin 1.5.28+ wizard for JavaCC 1.5.0+
 * TAREAS QUE HACE ESTO:
 *  Listar los nombres de todos los contextos, subcontextos (y argumentos si es que hay)
 	en el archivo de configuración.
 *  Contar el número de líneas de comentario.
 *  Identificar el contexto de primer nivel con más propiedades. 
 */
options
{
  static = false;
  DEBUG_TOKEN_MANAGER = false;
}

PARSER_BEGIN(practicaNginx)
import java.util.Stack;

public class practicaNginx
{
  public static void main(String args []) throws ParseException, java.io.FileNotFoundException
  {
    practicaNginxTokenManager tkmngr = null;
    //Entrada por fichero. Si no recibe nada salimos.
    if (args.length < 1)
    {
      System.out.println("P\u00e1sale un fichero.");
      System.exit(0);
    }
    else
    {
      tkmngr = new practicaNginxTokenManager(
      new SimpleCharStream(new java.io.FileInputStream(args [0]), 0, 0)
      );
    }
    Token tk;
    try
    {
      System.out.println("Diagrama de contextos y profundidad:");
      System.out.println("------------------------------------");
      while ((tk = tkmngr.getNextToken()).kind != EOF);
      //Impresión de estadísticas.
      System.out.println("------------------------------------");
      System.out.println("Lineas de comentario: " + tkmngr.lineasComentario);
      if(tkmngr.propsMain > tkmngr.maxNumeroPropiedades || tkmngr.contextoMasPropiedades.isEmpty()) {
		tkmngr.maxNumeroPropiedades = tkmngr.propsMain;
		tkmngr.contextoMasPropiedades = new String("main");
      }
      System.out.println("El contexto de primer nivel con m\u00e1s propiedades (" + tkmngr.maxNumeroPropiedades + ") es: " + tkmngr.contextoMasPropiedades);
    }
    catch (TokenMgrError x)
    {
      System.out.println("Exiting.");
      throw x;
    }
  }
}

PARSER_END(practicaNginx)

TOKEN_MGR_DECLS :
{
  int lineasComentario = 0;
  int numeroPropiedades = 0;
  int maxNumeroPropiedades = 0;
  int propsMain = 0;
  int depthLevel = 0;
  boolean listaSrv = false;
  String contextoMasPropiedades = new String("Ninguno.");
  String contextoActual = new String();
  Stack pilaEstados = new Stack();

  void tabular() {
	for(int i = 0; i < depthLevel; i++) {
		System.out.print("\t");
	}
  }
}

<* >
SKIP :
{
  "\r"
| "\t"
| " "
}

TOKEN :
{
  < #ID : ([ "a"-"z", "A"-"Z", "_", "/" ])+ >
| 	< #VALOR :
    (
      ~[ "\n", "\t", " ", ";", "{", "}", "#" ] (~[ ";", "{", "}", "#" ])*
    )
  |
    (
      "\'" (~[ "\'" ])+ "\'"
    ) >
| 	< #INICIO_CTXT : ("{") >
| 	< #FINAL_CTXT : ("}") >
| 	< #CONTEXTOS_SIMPLES :
    ("events")
  | ("http")
  | ("server")
  | ("mail") >
| 	< #CONTEXTOS_MULTI :
    ("location")
  | ("upstream")
  | ("if")
  | ("limit_except") >
}

<* >
MORE :
{
  < INICIO_COM : "#" >
  {
    pilaEstados.push(curLexState);
  }
  : COM
}

< COM >
TOKEN :
{
  < FIN_COMENTARIO_LINEA : "\n" >
  {
    lineasComentario++;
    SwitchTo((int) pilaEstados.pop());
  }
| < COSAS_COMENTADAS_LINEA : (~[ "\n" ])+ >
}

< DEFAULT >
TOKEN :
{
  < SIMPLE : < CONTEXTOS_SIMPLES > >
  {
    pilaEstados.push(curLexState);
    contextoActual = image.toString();
    if (listaSrv)
    {
      SwitchTo(ID_EN_MAIN);
    }
    else
    {
      tabular();
      System.out.println("Detectado contexto: " + image.toString());
      SwitchTo(NOMBRE_CON);
    }
  }
| < COMPLEJO : < CONTEXTOS_MULTI > >
  {
    if (image.toString().equals("upstream"))
    {
      listaSrv = true;
    }
    tabular();
    System.out.print("Detectado contexto: " + image.toString());
    pilaEstados.push(curLexState);
    contextoActual = image.toString();
  }
  : ARGS_CONTEXTO
| < CLAVE : < ID > >
  {
    propsMain++;
    pilaEstados.push(curLexState);
  }
  : ID_EN_MAIN
}

< DEFAULT, CUERPO_CONTEXTO, CUERPO_SUBCONTEXTO >
SKIP :
{
  "\n"
}

< ARGS_CONTEXTO >
TOKEN :
{
  < LITERALMENTE_CUALQUIER_COSA : (~[ "{" ])+ >
  {
    System.out.println(" con variables: " + image.toString());
  }
  : SUBCONTEXTO
}

< ARGS_SUBCONTEXTO >
TOKEN :
{
  < LITERALMENTE_CUALQUIER_COSA_2 : (~[ "{" ])+ >
  {
    System.out.println(" con variables: " + image.toString());
  }
  : SUBCONTEXTO
}

< ID_EN_MAIN, ID_EN_CTXT, ID_EN_SUBCTXT >
TOKEN :
{
  < VALOR_DE_CLAVE : < VALOR > >
  {
  }
  : VALOR_DONDE_SEA
}

< NOMBRE_CON >
TOKEN :
{
  < EMPEZAMOS : < INICIO_CTXT > >
  {
    numeroPropiedades = 0;
    depthLevel++;
  }
  : CUERPO_CONTEXTO
}

< SUBCONTEXTO >
TOKEN :
{
  < EMPEZAMOS_OTRA_VEZ : < INICIO_CTXT > >
  {
    depthLevel++;
  }
  : CUERPO_SUBCONTEXTO
}

< CUERPO_CONTEXTO >
TOKEN :
{
  < CONTEXTO_ANIDADO : < CONTEXTOS_SIMPLES > >
  {
    pilaEstados.push(curLexState);
    if (listaSrv && image.toString().equals("server"))
    {
      SwitchTo(ID_EN_CTXT);
    }
    else
    {
      tabular();
      System.out.println("Detectado subcontexto: " + image.toString());
      SwitchTo(SUBCONTEXTO);
    }
  }
| < CONTEXTO_ANIDADO_ARGS : < CONTEXTOS_MULTI > >
  {
    if (image.toString().equals("upstream"))
    {
      listaSrv = true;
    }
    tabular();
    System.out.print("Detectado subcontexto: " + image.toString());
    pilaEstados.push(curLexState);
  }
  : ARGS_SUBCONTEXTO
| < SALIDA : < FINAL_CTXT > >
  {
    if (numeroPropiedades > maxNumeroPropiedades)
    {
      maxNumeroPropiedades = numeroPropiedades;
      contextoMasPropiedades = new String(contextoActual);
    }
    contextoActual = new String();
    depthLevel--;
    SwitchTo((int) pilaEstados.pop());
  }
| < CLAVICORDIO : < ID > >
  {
    pilaEstados.push(curLexState);
    numeroPropiedades++;
  }
  : ID_EN_CTXT
}

< CUERPO_SUBCONTEXTO >
TOKEN :
{
  < MAS_ANIDACION_SIMPLE : < CONTEXTOS_SIMPLES > >
  {
    pilaEstados.push(curLexState);
    if (listaSrv && image.toString().equals("server"))
    {
      SwitchTo(ID_EN_SUBCTXT);
    }
    else
    {
      tabular();
      System.out.println("Detectado subcontexto: " + image.toString());
      SwitchTo(SUBCONTEXTO);
    }
  }
| < MAS_ANIDACION_COMPLEJA : < CONTEXTOS_MULTI > >
  {
    if (image.toString().equals("upstream"))
    {
      listaSrv = true;
    }
    tabular();
    System.out.print("Detectado subcontexto: " + image.toString());
    pilaEstados.push(curLexState);
  }
  : ARGS_SUBCONTEXTO
| < SUBCLAVE_L2 : < ID > >
  {
    pilaEstados.push(curLexState);
  }
  : ID_EN_SUBCTXT
| < SALIDA2 : < FINAL_CTXT > >
  {
    listaSrv = false;
    depthLevel--;
    SwitchTo((int) pilaEstados.pop());
  }
}

< VALOR_DONDE_SEA >
TOKEN :
{
  < FIN_VALOR : ";" >
  {
    SwitchTo((int) pilaEstados.pop());
  }
}
